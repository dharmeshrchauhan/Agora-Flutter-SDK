group 'io.agora.agora_rtc_engine'
version '1.0-SNAPSHOT'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  def kotlin_version = rootProject.ext.has('kotlin_version') ? rootProject.ext.get('kotlin_version') : '1.3.72'

  repositories {
    mavenCentral()
    google()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

rootProject.allprojects {
  repositories {
    mavenCentral()
    google()
    maven { url 'https://www.jitpack.io' }
    flatDir {
        dirs 'src/main/libs'
    }
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 29)
  buildToolsVersion safeExtGet('buildToolsVersion', '28.0.3')

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
    targetSdkVersion safeExtGet('targetSdkVersion', 29)

    consumerProguardFiles 'consumer-rules.pro'
  }

  sourceSets {
    main {
      java.srcDirs += 'src/main/kotlin'
      jniLibs.srcDirs = ['libs']
    }
  }

  lintOptions {
    disable 'InvalidPackage'
  }
}

dependencies {
  api 'com.github.agorabuilder:native-full-sdk:3.4.5'
  implementation files('libs/deepar.aar')
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${safeExtGet('kotlin_version', '1.3.72')}"
  implementation 'androidx.camera:camera-core:1.0.1'
  implementation 'androidx.camera:camera-lifecycle:1.0.1'
}
